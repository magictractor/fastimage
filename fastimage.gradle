plugins {
    id 'java-library'
    
    // https://docs.gradle.org/current/userguide/publishing_maven.html
    id 'maven-publish'
}

group = 'uk.co.magictractor'
version = '0.0.1-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        
            pom {
                name = "${project.name.capitalize()}"
                description = 'Faster getters and setters for RGB values in java.awt.BufferedImage instances.' +
                        ' Provides RGB classes that are used to get and set pixels in the image DataBuffers' +
                        ' without checking bounds and ignoring ColorSpaces.'
                url = "https://github.com/magictractor/${project.name}"
                inceptionYear = '2019'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'kend'
                        name = 'Ken Dobson'
                       // email = 'me@gmail.com'
                    }
                }
                scm {
                    // connection = 'scm:git:git:github.com/magictractor/${project.name}.git'
                    // developerConnection = 'scm:git:ssh://github.com/magictractor/${project.name}.git'
                    url = "https://github.com/magictractor/${project.name}"
                }
            }
        }
    }

    repositories {
        //maven {
        //    name = 'myRepo'
        //    url = layout.buildDirectory.dir("repo")
        //}
    }
}

// https://docs.gradle.org/current/userguide/declaring_repositories.html
repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    
    withSourcesJar()
    //withJavadocJar()
}

tasks.withType(Jar) {
    destinationDirectory.set(file("$rootDir/jars"))
}

clean {
    delete "$rootDir/jars"
}

// "libs.xxx" refers to libraries configured in version catalog in settings.gradle.
dependencies {
    // Logger API.
    implementation libs.slf4j.api
    // Logger implementation for unit tests and main() methods.
    runtimeOnly libs.logback.classic

    // MoreObjects is used for toString() implementations
    // Strings.padStart() is used when converting RGB to hex.
    implementation libs.guava
    
    testImplementation libs.junit.jupiter;
    testRuntimeOnly libs.junit.jupiter.platform;
    testImplementation libs.assertj;
}
